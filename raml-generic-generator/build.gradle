buildscript {
    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:1.2.3"
    }
}

plugins {
    id "de.undercouch.download" version "3.2.0"
}

apply plugin: 'application'
apply plugin: "com.github.johnrengelman.shadow"

import de.undercouch.gradle.tasks.download.Download

ext {
    apiProperty = 'api'
}

sourceSets {
    main {
        resources {
            srcDirs += "$buildDir/resources/main/raml"
        }
    }
    test {
        resources {
            srcDirs += "$buildDir/php"
        }
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile 'org.assertj:assertj-core:3.8.0'
    testCompile "org.spockframework:spock-core:${versions.spock}"

    compile project(':functional-utils')
    compile project(':raml-model')

    compile 'com.squareup:javapoet:1.9.0'
    compile 'commons-cli:commons-cli:1.4'
    compile 'commons-io:commons-io:2.5'
    compile 'org.antlr:stringtemplate:4.0.2'
    compile 'com.google.guava:guava:22.0'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.8.9'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.0'
    compile 'com.hypertino:inflector_2.12:1.0.7'
}

mainClassName = "io.vrap.rmf.raml.generic.generator.CodeGenerator"

task downloadCtpApiZipFile(type: Download) {
    src 'https://github.com/commercetools/commercetools-api-reference/archive/master.zip'
    dest new File(buildDir, 'commercetools-api-reference.zip')
    onlyIfNewer(true)
    overwrite(false)
}

task downloadAndUnzipCtpApiFile(dependsOn: downloadCtpApiZipFile, type: Copy) {
    from zipTree(downloadCtpApiZipFile.dest)
    into "$buildDir/resources/main/raml"
}

test.dependsOn(':raml-model:build')
build.dependsOn(downloadAndUnzipCtpApiFile)
downloadCtpApiZipFile.onlyIf { project.hasProperty(apiProperty) }
downloadAndUnzipCtpApiFile.onlyIf { project.hasProperty(apiProperty) }

shadowJar {
    append 'plugin.properties'
}
