plugins {
    id "de.undercouch.download" version "3.2.0"
}

import de.undercouch.gradle.tasks.download.Download

ext {
    ramlTckVersion = '1.1'
    tckProperty = 'tck'
}

sourceSets {
    test {
        resources {
            srcDirs += "$buildDir/resources/test/raml"
        }
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile 'com.tngtech.java:junit-dataprovider:1.12.0'
    testCompile 'org.assertj:assertj-core:3.8.0'
    testCompile "org.spockframework:spock-core:${versions.spock}"
    testCompile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.0'

    compile project(':raml-model')

}

task downloadTck1ZipFile(type: Download) {
    src 'https://github.com/raml-org/raml-tck/archive/v' + ramlTckVersion + '.zip'
    dest new File(buildDir, 'raml-tck-' + ramlTckVersion + '.zip')
    onlyIfNewer(true)
    overwrite(false)
}

task downloadTck2ZipFile(type: Download) {
    src 'https://github.com/raml-org/raml-tck/archive/wip/2.0.0.zip'
    dest new File(buildDir, 'raml-tck-2.0.zip')
    onlyIfNewer(true)
    overwrite(false)
}

task downloadCtpApiZipFile(type: Download) {
    src 'https://github.com/commercetools/commercetools-api-reference/archive/master.zip'
    dest new File(buildDir, 'commercetools-api-reference.zip')
    onlyIfNewer(true)
    overwrite(false)
}

tasks.withType(Test) {
    environment('RAML_TCK_VERSION', ramlTckVersion)
}

task downloadAndUnzipTck1File(dependsOn: downloadTck1ZipFile, type: Copy) {
    from zipTree(downloadTck1ZipFile.dest)
    into "$buildDir/resources/test/raml"
}

task downloadAndUnzipTck2File(dependsOn: downloadTck2ZipFile, type: Copy) {
    from zipTree(downloadTck2ZipFile.dest)
    into "$buildDir/resources/test/raml"
}

task downloadAndUnzipCtpApiFile(dependsOn: downloadCtpApiZipFile, type: Copy) {
    from zipTree(downloadCtpApiZipFile.dest)
    into "$buildDir/resources/test/raml"
}

test.dependsOn(downloadAndUnzipTck1File)
test.dependsOn(downloadAndUnzipTck2File)
test.dependsOn(downloadAndUnzipCtpApiFile)
test.dependsOn(':raml-model:build')

downloadTck1ZipFile.onlyIf { project.hasProperty(tckProperty) }
downloadTck2ZipFile.onlyIf { project.hasProperty(tckProperty) }
downloadCtpApiZipFile.onlyIf { project.hasProperty(tckProperty) }

downloadAndUnzipTck1File.onlyIf { project.hasProperty(tckProperty) }
downloadAndUnzipTck2File.onlyIf { project.hasProperty(tckProperty) }
downloadAndUnzipCtpApiFile.onlyIf { project.hasProperty(tckProperty) }
test.onlyIf { project.hasProperty(tckProperty) }
